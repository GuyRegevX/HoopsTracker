plugins {
    id 'org.springframework.boot' version '3.2.3' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'java'
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url "https://repo1.maven.org/maven2/"
            content {
                includeGroup "org.springframework"
            }
        }
        jcenter()
        google()
    }
    
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'hoops'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '21'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
        implementation 'org.postgresql:postgresql'
        
        // Add SpringDoc OpenAPI (Swagger) dependencies
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
        implementation 'org.springdoc:springdoc-openapi-starter-common:2.3.0'
        
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':hoops-api') {
    springBoot {
        mainClass = 'hoops.api.HoopsApiApplication'
    }
}

project(':hoops-ingest') {
    springBoot {
        mainClass = 'hoops.ingestion.HoopsIngestApplication'
    }
}

project(':hoops-common') {
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
}

// Root project should not produce a jar
tasks.named('jar') {
    enabled = false
}

wrapper {
    gradleVersion = '8.6'
} 